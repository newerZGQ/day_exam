apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

// 从本地 local.properties 中读取签名信息，避免在仓库中明文存储密码
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("local.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.gorden.dayexam"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 6
        versionName "1.0.5"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }


    signingConfigs {
        release {
            v1SigningEnabled true
            v2SigningEnabled true

            // 从 local.properties 中读取:
            // DAYEXAM_STORE_FILE, DAYEXAM_STORE_PASSWORD, DAYEXAM_KEY_ALIAS, DAYEXAM_KEY_PASSWORD
            def storeFilePath = keystoreProperties['DAYEXAM_STORE_FILE']
            if (storeFilePath != null && !storeFilePath.toString().trim().isEmpty()) {
                def keystoreFile = rootProject.file(storeFilePath)
                if (keystoreFile.exists()) {
                    keyAlias keystoreProperties['DAYEXAM_KEY_ALIAS']
                    storeFile keystoreFile
                    storePassword keystoreProperties['DAYEXAM_STORE_PASSWORD']
                    keyPassword keystoreProperties['DAYEXAM_KEY_PASSWORD']
                }
            }
        }
    }

    buildTypes {

        debug {
            applicationIdSuffix ".debug"
        }

        release {
            // Only use signing config if keystore exists
            def storeFilePath = keystoreProperties['DAYEXAM_STORE_FILE']
            if (storeFilePath != null && !storeFilePath.toString().trim().isEmpty()) {
                def keystoreFile = rootProject.file(storeFilePath)
                if (keystoreFile.exists()) {
                    signingConfig signingConfigs.release
                }
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation("androidx.viewpager2:viewpager2:1.0.0")
    implementation 'com.google.android.material:material:1.4.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def room_version = "2.4.3"
    implementation("androidx.room:room-runtime:$room_version")
    annotationProcessor "androidx.room:room-compiler:$room_version"
    // To use Kotlin annotation processing tool (kapt)
    kapt("androidx.room:room-compiler:$room_version")
    // optional - Kotlin Extensions and Coroutines support for Room
    implementation("androidx.room:room-ktx:$room_version")

    // apache poi依赖很混乱，最好不要改
    implementation 'javax.xml.stream:stax-api:1.0'
    implementation 'org.apache.poi:poi-ooxml:3.17'
    implementation 'org.apache.xmlbeans:xmlbeans:3.1.0'
    implementation 'com.fasterxml:aalto-xml:1.2.2'
    implementation 'com.jakewharton:disklrucache:2.0.2'

    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.jakewharton:disklrucache:2.0.2'

    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // LiveEventbus
    implementation 'io.github.jeremyliao:live-event-bus-x:1.8.0'

    // 图片缩放
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    // floating action button
    implementation "com.leinardi.android:speed-dial:3.2.0"
    // 自动换行布局
    implementation 'com.google.android.flexbox:flexbox:3.0.0'
    // 圆角图片
    implementation 'com.github.SheHuan:NiceImageView:1.0.5'
    // 压缩解压缩
    implementation 'net.lingala.zip4j:zip4j:2.6.0'
    // 设置页面包
    implementation 'androidx.preference:preference:1.1.1'

    // 引导
}